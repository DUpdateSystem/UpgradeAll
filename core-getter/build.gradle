import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.mozilla.rust-android-gradle.rust-android' version "0.9.6"
}

android {
    namespace 'net.xzos.upgradeall.getter'
    compileSdk = 36
    ndkVersion "26.3.11579264"

    defaultConfig {
        minSdk = 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_21
        }
    }
}

cargo {
    module = "./src/main/rust/api_proxy"
    targets = [
            "x86_64",
            "arm",
            "arm64"
    ]
    profile = gradle.startParameter.taskNames.any{it.toLowerCase().contains("debug")} ? "debug" : "release"
    libname = "api_proxy"

    features {
        all()
    }
}

dependencies {
    implementation "androidx.core:core-ktx:$android_ktx_version"
    implementation 'androidx.appcompat:appcompat:1.7.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

    implementation project(':core-getter:rpc')
    // Rust TLS
    implementation "rustls:rustls-platform-verifier:latest.release"
}

tasks.matching { it.name.matches(/merge.*JniLibFolders/) }.configureEach {
    it.inputs.dir(new File(buildDir, "rustJniLibs/android"))
    it.dependsOn("cargoBuild")
}