import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    // Using JitPack version with Gradle 9 compatibility and Android Studio deployment fixes
    id 'io.github.MatrixDev.android-rust' version '7494b71b357cbfe48241f4da76a551ea44cbd1fa'
}

android {
    namespace 'net.xzos.upgradeall.getter'
    compileSdk = 36
    ndkVersion '29.0.14206865'

    defaultConfig {
        minSdk = 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_21
        }
    }
}

androidRust {
    minimumSupportedRustVersion = "1.62.1"

    module("api_proxy") { moduleConfig ->
        moduleConfig.path = file("./src/main/rust/api_proxy")
        moduleConfig.targets = ["x86_64", "arm", "arm64"]

        moduleConfig.buildType("debug") {
            it.profile = "dev"
        }

        moduleConfig.buildType("release") {
            it.profile = "release"
            it.runTests = true
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:$android_ktx_version"
    implementation 'androidx.appcompat:appcompat:1.7.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

    implementation project(':core-getter:rpc')
    // Rust TLS
    implementation "rustls:rustls-platform-verifier:latest.release"
}