apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'

sourceSets {
    main {
        java {
            srcDirs "${protobuf.generatedFilesBaseDir}/main/java"
            srcDirs "${protobuf.generatedFilesBaseDir}/main/grpc"
        }
    }
}

def grpc_version = '1.29.0'
protobuf {
    protoc { artifact = "com.google.protobuf:protoc:$protoc_version" }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // jvm 反射
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'
    // 数据处理
    implementation 'com.google.code.gson:gson:2.8.6'
    // 字符串匹配；日志打印去除转义字符
    implementation 'org.apache.commons:commons-text:1.8'
    // 版本比较
    implementation 'org.apache.maven:maven-artifact:3.6.3'
    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:4.7.2'
    implementation 'com.squareup.okio:okio:2.7.0-alpha.lockfree.1'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.7.2'

    // grpc
    implementation "io.grpc:grpc-okhttp:$grpc_version"
    implementation "io.grpc:grpc-protobuf-lite:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"
    implementation "com.google.protobuf:protobuf-javalite:$protoc_version"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}
