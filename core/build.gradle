apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.protobuf'

def grpc_version = '1.31.1'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java {
                srcDirs "${protobuf.generatedFilesBaseDir}/main/java"
                srcDirs "${protobuf.generatedFilesBaseDir}/main/grpc"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    protobuf {
        protoc { artifact = "com.google.protobuf:protoc:${rootProject.ext.depend["protoc"]}" }
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
            }
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    java { option 'lite' }
                }
                task.plugins {
                    grpc { // Options added to --grpc_out
                        option 'lite'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // jvm 反射
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${rootProject.ext.depend["coroutines"]}"

    implementation "androidx.core:core-ktx:${rootProject.ext.depend["androidx_core"]}"
    implementation "androidx.appcompat:appcompat:${rootProject.ext.depend["appcompat"]}"
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // 数据处理
    implementation "com.google.code.gson:gson:${rootProject.ext.depend["gson"]}"
    // 字符串匹配；日志打印去除转义字符
    implementation "org.apache.commons:commons-text:${rootProject.ext.depend["apache_commons_text"]}"
    // 版本比较
    implementation "org.apache.maven:maven-artifact:${rootProject.ext.depend["apache_maven_artifact"]}"
    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:${rootProject.ext.depend["okhttp"]}"

    // grpc
    implementation "io.grpc:grpc-okhttp:$grpc_version"
    implementation "io.grpc:grpc-protobuf-lite:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"
    implementation "com.google.protobuf:protobuf-javalite:${rootProject.ext.depend["protoc"]}"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}
