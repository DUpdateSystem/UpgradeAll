apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
// Firebase
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    signingConfigs {
        main {
            def propsFile = rootProject.file('keystore.properties')
            def props = new Properties()
            props.load(new FileInputStream(propsFile))

            storeFile file(props['STORE_FILE_PATH'])
            storePassword props['STORE_PASSWORD']
            keyPassword props['KEY_PASSWORD']
            keyAlias = props['KEY_ALIAS']
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId "net.xzos.upgradeall"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 48
        versionName "0.1.4.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.main
            manifestPlaceholders = [enableCrashReporting: "true", appName: "UpgradeAll"]
        }
        debug {
            signingConfig signingConfigs.main
            manifestPlaceholders = [enableCrashReporting: "false", appName: "UpgradeAll-β"]
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    android.buildFeatures.viewBinding = true  // 视图绑定
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.drawerlayout:drawerlayout:1.1.1"

    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'

    implementation "com.google.protobuf:protobuf-javalite:$protoc_version"
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9'

    // database
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    // Test helpers
    testImplementation "androidx.room:room-testing:$room_version"


    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:4.8.1'
    implementation 'com.squareup.okio:okio:2.8.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.8.1'

    // Firebase
    implementation 'com.google.firebase:firebase-core:17.5.0'
    implementation 'com.google.firebase:firebase-perf:19.0.8'
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'
    configurations.compile.exclude group: 'com.google.protobuf', module: 'protobuf-lite'

    // Aria
    implementation 'com.arialyy.aria:core:3.8.8'
    kapt 'com.arialyy.aria:compiler:3.8.8'

    // WebDav
    implementation 'com.thegrizzlylabs.sardine-android:sardine-android:0.7'

    // Shizuku
    implementation 'moe.shizuku.privilege:api:3.1.0'

    // 数据处理
    implementation 'com.jonathanfinerty.once:once:1.3.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    // 字符串匹配；日志打印去除转义字符
    implementation 'org.apache.commons:commons-text:1.9'
    // 版本比较
    implementation 'org.apache.maven:maven-artifact:3.6.3'

    // shell
    implementation 'eu.darken.rxshell:core:1.0.9'
    implementation 'eu.darken.rxshell:root:1.0.9'

    // 图片加载
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.github.yalantis:ucrop:2.2.4'  // 图片裁剪

    // 界面设计
    // Google MD 库
    implementation 'com.google.android.material:material:1.3.0-alpha02'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.github.kobakei:MaterialFabSpeedDial:master-SNAPSHOT'
    // svg 单个 path 颜色切换
    implementation 'com.github.devsideal:VectorChildFinder:1.0.0'

    //Toast BadTokenException on 7.1.1
    implementation 'me.drakeet.support:toastcompat:1.1.0'

    //RecyclerView
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'

    implementation project(path: ':core')
}
