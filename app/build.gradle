plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.android'
}

// NO FREE
if (!project.hasProperty('free')) {
    // Firebase
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.firebase.firebase-perf'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "net.xzos.upgradeall"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 104
        versionName "0.20-alpha.1"
        if (project.hasProperty('appVerName')) {
            versionName = "${versionName}_${appVerName}"
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
                arg("room.incremental", "true")
            }
        }
        vectorDrawables {
            useSupportLibrary true
        }
    }
    buildTypes {
        release {
            //minifyEnabled true
            minifyEnabled false
            zipAlignEnabled true
            //shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashReporting: "true", appName: "UpgradeAll"]
        }
        debug {
            minifyEnabled false
            zipAlignEnabled true
            manifestPlaceholders = [enableCrashReporting: "false", appName: "UpgradeAll-β"]
            applicationIdSuffix ".debug"
        }
    }
    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            if (variant.buildType.name == 'release') {
                outputFileName = "UpgradeAll_${versionName}.apk"
            }
            if (variant.buildType.name == 'debug') {
                outputFileName = "UpgradeAll_${versionName}-debug.apk"
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
        freeCompilerArgs = ["-Xopt-in=kotlin.RequiresOptIn", "-XXLanguage:+InlineClasses"]
    }

    buildFeatures {
        // 视图绑定
        dataBinding true
        viewBinding true
        compose true
    }

    dependenciesInfo.includeInApk false
    lint {
        abortOnError true
        checkReleaseBuilds false
    }
    namespace 'net.xzos.upgradeall'
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.3'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation "androidx.drawerlayout:drawerlayout:1.2.0"
    implementation 'androidx.viewpager2:viewpager2:1.1.0-beta02'

    implementation 'androidx.activity:activity-ktx:1.8.0'
    implementation 'androidx.fragment:fragment-ktx:1.6.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.4'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.4'

    // Kotlin
    implementation 'androidx.core:core-ktx:1.12.0'
    //noinspection DifferentStdlibGradleVersion
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    implementation 'androidx.activity:activity-compose:1.8.0'
    implementation 'androidx.compose.ui:ui-viewbinding:1.5.4'
    implementation platform('androidx.compose:compose-bom:2023.10.01')
    implementation 'androidx.compose.ui:ui:1.5.4'
    implementation 'androidx.compose.ui:ui-graphics:1.5.4'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.5.4'
    implementation 'androidx.compose.material3:material3:1.1.2'
    androidTestImplementation platform('androidx.compose:compose-bom:2023.10.01')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.5.4'

    // Java desugar
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.4.6'
    debugImplementation 'androidx.compose.ui:ui-tooling:1.5.4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest:1.5.4'

    // WorkManager
    def work_version = '2.8.1'
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Localization
    implementation 'com.akexorcist:localization:1.2.11'

    // 数据处理
    implementation 'com.jonathanfinerty.once:once:1.3.1'

    // 图片加载
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    kapt 'com.github.bumptech.glide:compiler:4.16.0'

    // 界面设计
    // Google MD 库
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.github.kobakei:MaterialFabSpeedDial:2.0.0'
    // svg 单个 path 颜色切换
    implementation 'com.github.devsideal:VectorChildFinder:1.0.0'
    implementation 'dev.rikka.rikkax.insets:insets:1.3.0'
    implementation 'dev.rikka.rikkax.layoutinflater:layoutinflater:1.3.0'

    //RecyclerView
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.11'

    // 日历
    implementation 'com.github.6tail:lunar-java:1.6.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation project(path: ':app-backup')
    implementation project(path: ':core-android-utils')
    implementation project(path: ':core')
    implementation project(path: ':core-utils')
    implementation project(path: ':core-downloader')
    implementation project(path: ':core-installer')

    // NO FREE
    if (!project.hasProperty('free')) {
        // Firebase
        implementation 'com.google.firebase:firebase-perf:20.3.1'
        implementation 'com.google.firebase:firebase-analytics:21.2.2'
        implementation 'com.google.firebase:firebase-crashlytics:18.3.6'
    }
    //Protobuf
    implementation 'com.google.protobuf:protobuf-java:3.24.4'
}
// fix different protobuf versions of gplayapi and firebase
configurations {
    all*.exclude group: 'com.google.protobuf', module: 'protobuf-javalite'
    all*.exclude group: 'com.google.firebase', module: 'protolite-well-known-types'
}
