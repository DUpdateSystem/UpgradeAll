import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.ksp)
    alias(libs.plugins.legacy.kapt)
    alias(libs.plugins.kotlin.compose)
}

// NO FREE
if (!project.hasProperty('free')) {
    // Firebase
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.firebase.firebase-perf'
}

android {
    compileSdk = 36

    defaultConfig {
        applicationId "net.xzos.upgradeall"
        minSdk = 23
        targetSdk = 36
        versionCode = 105
        versionName = "0.20-alpha.4"
        if (project.hasProperty('appVerName')) {
            versionName = "${versionName}_${appVerName}"
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
            arg("room.incremental", "true")
        }
        vectorDrawables {
            useSupportLibrary = true
        }
    }
    buildTypes {
        release {
            // minifyEnabled true
            // shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashReporting: "true", appName: "UpgradeAll"]
        }
        debug {
            minifyEnabled = false
            manifestPlaceholders = [enableCrashReporting: "false", appName: "UpgradeAll-Î²"]
            applicationIdSuffix ".debug"
            packaging {
                jniLibs {
                    keepDebugSymbols += "**/*.so"
                }
            }
        }
    }
    // APK output file naming is configured via androidComponents block below
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
        coreLibraryDesugaringEnabled = true
    }

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_21
            freeCompilerArgs = ["-opt-in=kotlin.RequiresOptIn"]
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    dependenciesInfo.includeInApk = false
    lint {
        abortOnError = true
        checkReleaseBuilds = false
    }
    namespace = 'net.xzos.upgradeall'

    packaging {
        resources {
            pickFirsts += 'META-INF/DEPENDENCIES'
        }
    }
}

androidComponents {
    onVariants(selector().all()) { variant ->
        variant.outputs.forEach { output ->
            def appVersion = android.defaultConfig.versionName
            if (project.hasProperty('appVerName')) {
                appVersion = "${appVersion}_${project.property('appVerName')}"
            }
            if (variant.buildType == 'release') {
                output.outputFileName.set("UpgradeAll_${appVersion}.apk")
            } else if (variant.buildType == 'debug') {
                output.outputFileName.set("UpgradeAll_${appVersion}-debug.apk")
            }
        }
    }
}

dependencies {
    // Core library desugaring for Java 8+ API support on older Android versions
    coreLibraryDesugaring libs.desugar.jdk.libs

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation libs.androidx.appcompat
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.preference.ktx
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.recyclerview
    implementation libs.androidx.drawerlayout
    implementation libs.androidx.viewpager2
    implementation libs.androidx.swiperefreshlayout
    implementation libs.androidx.documentfile

    implementation libs.androidx.activity.ktx
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx

    // Kotlin
    implementation libs.androidx.core.ktx
    implementation libs.kotlin.stdlib
    implementation libs.kotlinx.coroutines.android
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation libs.androidx.compose.ui.viewbinding

    // Compose (versions managed by BOM)
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.graphics
    implementation libs.androidx.compose.ui.tooling.preview
    implementation libs.androidx.compose.material3
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.compose.ui.test.junit4
    debugImplementation libs.androidx.compose.ui.tooling
    debugImplementation libs.androidx.compose.ui.test.manifest

    // WorkManager
    implementation libs.androidx.work.runtime.ktx

    // Localization
    implementation libs.localization

    implementation libs.once

    // Image loading
    implementation libs.glide
    ksp libs.glide.ksp

    // Material Design
    implementation libs.material
    implementation libs.androidx.cardview
    implementation libs.materialFabSpeedDial
    implementation(libs.vectorChildFinder) {
        exclude group: 'com.android.support'
    }
    implementation libs.rikkax.insets
    implementation libs.rikkax.layoutinflater

    // RecyclerView
    implementation libs.baseRecyclerViewAdapterHelper

    // Calendar
    implementation libs.lunar.java

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.espresso.core

    implementation project(':app-backup')
    implementation project(':core-android-utils')
    implementation project(':core')
    implementation project(':core-utils')
    implementation project(':core-downloader')
    implementation project(':core-installer')

    // NO FREE
    if (!project.hasProperty('free')) {
        // Firebase
        implementation libs.firebase.perf
        implementation libs.firebase.analytics
        implementation libs.firebase.crashlytics
    }
    // Protobuf
    implementation libs.protobuf.java
}
// fix different protobuf versions of gplayapi and firebase
configurations.all {
    exclude group: 'com.google.protobuf', module: 'protobuf-javalite'
    exclude group: 'com.google.firebase', module: 'protolite-well-known-types'
}
